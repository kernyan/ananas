OUTPUT_ARCH(i386:x86-64)
ENTRY(__entry)
SECTIONS {
	.text 0xffffffff80100000 : AT(0x100000) {
		text = .;
		*(.text)
		*(.text*)
		*(.rodata)
		*(.rodata*)
		__rodata_end = ALIGN(4096);
	}
	.data : {
		*(.data)
		*(.data*)
		/*
		 * Init/exit definitions after the r/w data; we need to sort
		 * them and thus be able to write them.
		 */
		__initfuncs_begin = ALIGN(8);
		*(initfuncs)
		__initfuncs_end = .;
		__exitfuncs_begin = ALIGN(8);
		*(exitfuncs)
		__exitfuncs_end = .;
		. = ALIGN(4096);
	}
	.bss : {
		*(.bss)
		*(.bss*)
		*(COMMON)
		. = ALIGN(4096);
	}
	__end = . ;
	__traceid_begin = .;
	.traceids : {
		*(.traceids)
	}
	__traceid_end = .;
	.tracenames : {
		*(.tracenames)
	}
	/DISCARD/ : {
		*(.eh_frame)
	}
}
